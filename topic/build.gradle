def config = rootProject.ext
if (isTopicRunAlone.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
//apply plugin: 'com.android.library'
android {
    compileSdkVersion config.android.compileSdkVersion
    buildToolsVersion config.android.buildToolsVersion
    defaultConfig {
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }

        if (isTopicRunAlone.toBoolean()){
            applicationId "com.apache.fastandroid.topic"
            //dex突破65535的限制
            multiDexEnabled true
        }
        minSdkVersion config.android.minSdkVersion
        targetSdkVersion config.android.targetSdkVersion
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // 替换Manifest里${PROVIDER_B}占位符
            manifestPlaceholders = [PROVIDER_TOPIC: 'PROVIDER_TOPIC']
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [PROVIDER_TOPIC: 'PROVIDER_TOPIC_DEBUG']
        }
    }

    resourcePrefix "topic_"

    sourceSets {
        main {
            if (isTopicRunAlone.toBoolean()) {
                manifest.srcFile 'src/main/runalone/AndroidManifest.xml'
                java.srcDirs = ['src/main/java', 'src/main/runalone/java']
                res.srcDirs = ['src/main/res', 'src/main/runalone/res']
                assets.srcDirs = ['src/main/assets', 'src/main/runalone/assets']
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    exclude '**/runalone/**'
                }
            }
        }
    }


    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    api config.supportDependencies.multidex
    api project(':baselib')


}
