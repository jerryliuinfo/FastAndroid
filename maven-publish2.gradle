project.plugins.apply('maven-publish')
project.plugins.apply('signing')

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())



def publishedGroupId='io.github.jerryliuinfo'
def publishedArtifactId='FastFramework-library'
def publishedVersion='1.0.2'


def libraryName = 'FastFramework-library'
def libraryDescription = 'Simply defining adapters of RecyclerView in Android'

def gitUrl='https://github.com/jerryliuinfo/FastAndroid.git'
def siteUrl='https://github.com/jerryliuinfo/FastAndroid'


def developerId='JerryLiu@3502'
def developerName='JerryLiu'
def developerEmail='liuxiangxiang1234@163.com'


def RELEASE_REPOSITORY_URL = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
def SNAPSHOT_REPOSITORY_URL = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'


afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = publishedGroupId
                artifactId = publishedArtifactId
                version = publishedVersion

                from components.release
                // generate pom.xml
                pom {
                    name = libraryName
                    description = libraryDescription
                    url = siteUrl

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = developerId
                            name = developerName
                            email = developerEmail
                        }
                    }
                    scm {
                        url = siteUrl
                    }
                }
            }

        }

        repositories {
            maven {
                // change to point to your repo
                //url = publishing.publications.release.version.endsWith('SNAPSHOT') ? snapshotRepo : releaseRepo
                url = publishedVersion.endsWith('SNAPSHOT') ? SNAPSHOT_REPOSITORY_URL : RELEASE_REPOSITORY_URL
                println "url: ${url}"
                credentials {
                    username = properties.getProperty('credencial.username')
                    password = properties.getProperty('credencial.password')
                }
            }
            //本地仓库
            maven {
                name 'Build'
                url = layout.buildDirectory.dir('repo')
            }
        }
    }

//    if(project.properties['signing.keyId']&&project.properties['signing.password']&&project.properties['signing.secretKeyRingFile']){
    if(properties.getProperty('signing.keyId')&&properties.getProperty('signing.password')&&properties.getProperty('signing.secretKeyRingFile')){
        signing {
            sign publishing.publications.release
        }
    }
}